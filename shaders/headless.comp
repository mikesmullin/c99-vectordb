#version 450

layout(local_size_x = 256) in;

layout(std430, binding = 0) readonly buffer WeightsBuffer {
    float data[];
} weights;

layout(std430, binding = 1) readonly buffer InputBuffer {
    float data[];
} inputVec;

layout(std430, binding = 2) writeonly buffer OutputBuffer {
    float data[];
} outputVec;

layout(push_constant) uniform Constants {
    uint weight_offset; // Offset in floats
    uint n; // Input dimension
    uint d; // Output dimension
} params;

void main() {
    uint row = gl_GlobalInvocationID.x;
    if (row >= params.d) return;

    float sum = 0.0;
    uint n = params.n;
    uint offset = params.weight_offset + row * n;

    for (uint i = 0; i < n; i++) {
        sum += weights.data[offset + i] * inputVec.data[i];
    }

    outputVec.data[row] = sum;
}
